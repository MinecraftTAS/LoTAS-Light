java {
	sourceCompatibility = targetCompatibility = 21
	withSourcesJar()
}

version = project.mod_version
group = project.maven_group

base { 
	archivesName = "${mod_name}-${minecraft_version}"
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
}

loom {
	// set access widener
	def accessWidenerFile = file('src/main/resources/lotaslight.accesswidener')
	if(accessWidenerFile.exists()) {
		accessWidenerPath = accessWidenerFile
	}
	
	// set custom log4jconfig
	def log4jFile = file('src/main/resources/log4j.xml')
	if(log4jFile.exists()) {
		log4jConfigs.from(log4jFile)
	}
}

processResources {
	inputs.property "version", project.version
	inputs.property "allowedMcVersion", project.allowed_mc_version

	filesMatching("fabric.mod.json") { file -> 
		expand "version": project.version
        filter { String line ->
            line.replace('"minecraft": "*",', '"minecraft": "' + project.allowed_mc_version + '",')
        };
	}
}

modrinth {
    token = project.modrinthtoken
    projectId = "lotas-light"
    versionType = "release"
    uploadFile = remapJar
    loaders = ["fabric"]
    dependencies {
        required.project "fabric-api"
    }
}
